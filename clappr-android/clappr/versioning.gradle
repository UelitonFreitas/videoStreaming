ext {
    def version_regex = "\\d{1,3}\\.\\d{1,3}\\.\\d{1,3}"
    def version_snapshot_regex = "-SNAP[-\\w]*-\\d{14}"
    def version_rc_regex = "-RC-\\d{14}"
    def RC = "RC"
    def RELEASE = "RELEASE"
    def SNAPSHOT = "SNAPSHOT"

    buildVersionType = { version ->
        if (version.toUpperCase().contains("SNAPSHOT")) {
            throw new Exception("Reserved word \"SNAPSHOT\" can not be used in the version name")
        } else if (version.matches(version_regex + version_snapshot_regex)) {
            SNAPSHOT;
        } else if (version.matches(version_regex + version_rc_regex)) {
            RC;
        } else if (version.matches(version_regex)) {
            RELEASE;
        } else {
            throw new Exception("Wrong version format \"$version\"")
        }
    }
    buildPublishAttrs = { version ->
        def versionType = buildVersionType(version)

        def bintrayRepoName = (versionType == SNAPSHOT)? 'clappr-android-snapshot' : 'clappr-android-release'
        def bintrayVersionDesc = (versionType == SNAPSHOT)? 'Clappr snapshot' : (versionType == RC)? 'Clappr candidate alpha version': 'Clappr alpha version'
        def bintrayPublishNonDefault = (versionType == RELEASE)? false : true
        println "Building $bintrayVersionDesc ($version) as $versionType to publish on $bintrayRepoName"

        [
                versionName: version,
                versionType: versionType,
                bintrayRepoName: bintrayRepoName,
                bintrayVersionDesc: bintrayVersionDesc,
                bintrayPublishNonDefault: bintrayPublishNonDefault
        ];
    }
}
